apply plugin: 'com.android.application'

apply plugin: 'jacoco'

jacoco {
    toolVersion = '0.8.5'
}

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "de.wohlfrom.presenter"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 4
        versionName "2.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            testCoverageEnabled = true
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions.unitTests.all {
        testLogging {
            exceptionFormat = 'full'
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            showStandardStreams = true
        }
    }

    testOptions.unitTests.includeAndroidResources = true

    // Increase timeout for adb operations in slow environments like travis ci
    adbOptions {
        timeOutInMs 10 * 60 * 1000  // 10 minutes
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.fragment:fragment:1.2.5'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:rules:1.3.0'

    // Espresso dependencies
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    testImplementation 'androidx.test.ext:junit:1.1.2'
    testImplementation 'org.robolectric:robolectric:4.4'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'

    // For fragment testing
    debugImplementation 'androidx.fragment:fragment-testing:1.2.5'
    configurations.all {
        resolutionStrategy {
            force "androidx.test:monitor:1.3.0"
        }
    }
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

task jacocoTestReport(type: JacocoReport,
        dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {
    group "verification"

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*',
                      '**/*Test*.*', 'android/**']
    def debugTree = fileTree(dir: "$project.buildDir/intermediates/javac/debug/" +
                "compileDebugJavaWithJavac/classes",
            excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    getSourceDirectories().setFrom(files([mainSrc]))
    getClassDirectories().setFrom(files([debugTree]))
    getExecutionData().setFrom(fileTree(dir: project.buildDir, includes: [
            'jacoco/*.exec', 'outputs/code-coverage/connected/*coverage.ec'
    ]))
}